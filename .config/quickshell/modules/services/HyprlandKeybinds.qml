import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Hyprland
import qs.config
import qs.modules.globals

QtObject {
    id: root

    property Process hyprctlProcess: Process {}

    property var previousVibeshellBinds: ({})
    property var previousCustomBinds: []
    property bool hasPreviousBinds: false

    property Timer applyTimer: Timer {
        interval: 100
        repeat: false
        onTriggered: applyKeybindsInternal()
    }

    function applyKeybinds() {
        applyTimer.restart();
    }

    // Helper function to check if an action is compatible with the current layout
    function isActionCompatibleWithLayout(action) {
        // If no compositor specified, action works everywhere
        if (!action.compositor)
            return true;

        // If compositor type is not hyprland, skip (future-proofing)
        if (action.compositor.type && action.compositor.type !== "hyprland")
            return false;

        // If no layouts specified or empty array, action works in all layouts
        if (!action.compositor.layouts || action.compositor.layouts.length === 0)
            return true;

        // Check if current layout is in the allowed list
        const currentLayout = GlobalStates.hyprlandLayout;
        return action.compositor.layouts.indexOf(currentLayout) !== -1;
    }

    function cloneKeybind(keybind) {
        return {
            modifiers: keybind.modifiers ? keybind.modifiers.slice() : [],
            key: keybind.key || ""
        };
    }

    function storePreviousBinds() {
        if (!Config.keybindsLoader.loaded)
            return;

        const vibeshell = Config.keybindsLoader.adapter.vibeshell;

        // Store dashboard keybinds
        previousVibeshellBinds = {
            dashboard: {
                widgets: cloneKeybind(vibeshell.dashboard.widgets),
                clipboard: cloneKeybind(vibeshell.dashboard.clipboard),
                emoji: cloneKeybind(vibeshell.dashboard.emoji),
                tmux: cloneKeybind(vibeshell.dashboard.tmux),
                wallpapers: cloneKeybind(vibeshell.dashboard.wallpapers),
                notes: cloneKeybind(vibeshell.dashboard.notes)
            },
            system: {
                overview: cloneKeybind(vibeshell.system.overview),
                powermenu: cloneKeybind(vibeshell.system.powermenu),
                config: cloneKeybind(vibeshell.system.config),
                lockscreen: cloneKeybind(vibeshell.system.lockscreen),
                tools: cloneKeybind(vibeshell.system.tools),
                screenshot: cloneKeybind(vibeshell.system.screenshot),
                screenrecord: cloneKeybind(vibeshell.system.screenrecord),
                lens: cloneKeybind(vibeshell.system.lens),
                reload: vibeshell.system.reload ? cloneKeybind(vibeshell.system.reload) : null,
                quit: vibeshell.system.quit ? cloneKeybind(vibeshell.system.quit) : null
            }
        };

        // Store custom keybinds
        const customBinds = Config.keybindsLoader.adapter.custom;
        previousCustomBinds = [];
        if (customBinds && customBinds.length > 0) {
            for (let i = 0; i < customBinds.length; i++) {
                const bind = customBinds[i];
                if (bind.keys) {
                    let keys = [];
                    for (let k = 0; k < bind.keys.length; k++) {
                        keys.push(cloneKeybind(bind.keys[k]));
                    }
                    previousCustomBinds.push({
                        keys: keys
                    });
                } else {
                    previousCustomBinds.push(cloneKeybind(bind));
                }
            }
        }

        hasPreviousBinds = true;
    }

    function applyKeybindsInternal() {
        // Verify that the adapter is charged
        if (!Config.keybindsLoader.loaded) {
            console.log("HyprlandKeybinds: Esperando que se cargue el adapter...");
            return;
        }

        // Wait for the layout to be ready
        if (!GlobalStates.hyprlandLayoutReady) {
            console.log("HyprlandKeybinds: Esperando que se detecte el layout de Hyprland...");
            return;
        }

        console.log("HyprlandKeybinds: Aplicando keybindings (layout: " + GlobalStates.hyprlandLayout + ")...");

        // Construir lista de unbinds
        let unbindCommands = [];

        // Helper function para formatear modifiers
        function formatModifiers(modifiers) {
            if (!modifiers || modifiers.length === 0)
                return "";
            return modifiers.join(" + ");
        }

        function luaString(value) {
            return JSON.stringify(value || "");
        }

        function luaKeyString(keybind) {
            const mods = formatModifiers(keybind.modifiers);
            return mods ? `${mods} + ${keybind.key}` : keybind.key;
        }

        function directionName(direction) {
            const directions = {
                l: "left",
                r: "right",
                u: "up",
                d: "down"
            };
            return directions[direction] || direction;
        }

        function parseDelta(argument) {
            const parts = (argument || "0 0").trim().split(/\s+/);
            const x = Number(parts[0] || 0);
            const y = Number(parts[1] || 0);
            return {
                x: isNaN(x) ? 0 : x,
                y: isNaN(y) ? 0 : y
            };
        }

        function luaBindOptions(flags) {
            let options = [];

            if (flags && flags.indexOf("l") !== -1)
                options.push("locked = true");
            if (flags && flags.indexOf("e") !== -1)
                options.push("repeating = true");
            if (flags && flags.indexOf("r") !== -1)
                options.push("release = true");
            if (flags && flags.indexOf("m") !== -1)
                options.push("mouse = true");

            return options.length > 0 ? `, { ${options.join(", ")} }` : "";
        }

        function luaDispatcher(dispatcher, argument, flags) {
            switch (dispatcher) {
            case "exec":
                return `hl.dsp.exec_cmd(${luaString(argument)})`;
            case "killactive":
                return "hl.dsp.window.close()";
            case "workspace":
                return `hl.dsp.focus({ workspace = ${luaString(argument)} })`;
            case "movetoworkspace":
                return `hl.dsp.window.move({ workspace = ${luaString(argument)} })`;
            case "movefocus":
                return `hl.dsp.focus({ direction = ${luaString(directionName(argument))} })`;
            case "movewindow":
                if (flags && flags.indexOf("m") !== -1 && !argument)
                    return "hl.dsp.window.drag()";
                return `hl.dsp.window.move({ direction = ${luaString(directionName(argument))} })`;
            case "resizewindow":
                if (flags && flags.indexOf("m") !== -1 && !argument)
                    return "hl.dsp.window.resize()";
                {
                    const delta = parseDelta(argument);
                    return `hl.dsp.window.resize({ x = ${delta.x}, y = ${delta.y}, relative = true })`;
                }
            case "resizeactive":
                {
                    const delta = parseDelta(argument);
                    return `hl.dsp.window.resize({ x = ${delta.x}, y = ${delta.y}, relative = true })`;
                }
            case "layoutmsg":
                return `hl.dsp.layout(${luaString(argument)})`;
            case "togglespecialworkspace":
                return `hl.dsp.workspace.toggle_special(${luaString(argument)})`;
            default:
                return `hl.dsp.exec_cmd(${luaString(`hyprctl dispatch ${dispatcher}${argument ? " " + argument : ""}`)})`;
            }
        }

        // Helper function para crear un bind command for Hyprland's Lua parser
        function createBindCommand(keybind, flags) {
            const dispatcher = keybind.dispatcher;
            const argument = keybind.argument || "";
            return `eval hl.bind(${luaString(luaKeyString(keybind))}, ${luaDispatcher(dispatcher, argument, flags)}${luaBindOptions(flags)})`;
        }

        // Helper function para crear un unbind command
        function createUnbindCommand(keybind) {
            return `eval hl.unbind(${luaString(luaKeyString(keybind))})`;
        }

        // Helper function para crear unbind command desde key object (new format)
        function createUnbindFromKey(keyObj) {
            return `eval hl.unbind(${luaString(luaKeyString(keyObj))})`;
        }

        // Helper function para crear bind command desde key + action (new format)
        function createBindFromKeyAction(keyObj, action) {
            const dispatcher = action.dispatcher;
            const argument = action.argument || "";
            const flags = action.flags || "";
            return `eval hl.bind(${luaString(luaKeyString(keyObj))}, ${luaDispatcher(dispatcher, argument, flags)}${luaBindOptions(flags)})`;
        }

        // Build batch command with all binds
        let batchCommands = [];

        // First, unbind previous keybinds if we have them stored
        if (hasPreviousBinds) {
            // Unbind previous vibeshell dashboard keybinds
            if (previousVibeshellBinds.dashboard) {
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.dashboard.widgets));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.dashboard.clipboard));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.dashboard.emoji));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.dashboard.tmux));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.dashboard.wallpapers));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.dashboard.notes));
            }

            // Unbind previous vibeshell system keybinds
            if (previousVibeshellBinds.system) {
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.system.overview));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.system.powermenu));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.system.config));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.system.lockscreen));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.system.tools));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.system.screenshot));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.system.screenrecord));
                unbindCommands.push(createUnbindCommand(previousVibeshellBinds.system.lens));
                if (previousVibeshellBinds.system.reload) unbindCommands.push(createUnbindCommand(previousVibeshellBinds.system.reload));
                if (previousVibeshellBinds.system.quit) unbindCommands.push(createUnbindCommand(previousVibeshellBinds.system.quit));
            }

            // Unbind previous custom keybinds
            for (let i = 0; i < previousCustomBinds.length; i++) {
                const prev = previousCustomBinds[i];
                if (prev.keys) {
                    for (let k = 0; k < prev.keys.length; k++) {
                        unbindCommands.push(createUnbindFromKey(prev.keys[k]));
                    }
                } else {
                    unbindCommands.push(createUnbindCommand(prev));
                }
            }
        }

        // Procesar Vibeshell keybinds (still use old format)
        const vibeshell = Config.keybindsLoader.adapter.vibeshell;

        // Dashboard keybinds
        const dashboard = vibeshell.dashboard;
        unbindCommands.push(createUnbindCommand(dashboard.widgets));
        unbindCommands.push(createUnbindCommand(dashboard.clipboard));
        unbindCommands.push(createUnbindCommand(dashboard.emoji));
        unbindCommands.push(createUnbindCommand(dashboard.tmux));
        unbindCommands.push(createUnbindCommand(dashboard.wallpapers));
        unbindCommands.push(createUnbindCommand(dashboard.notes));

        batchCommands.push(createBindCommand(dashboard.widgets, dashboard.widgets.flags || ""));
        batchCommands.push(createBindCommand(dashboard.clipboard, dashboard.clipboard.flags || ""));
        batchCommands.push(createBindCommand(dashboard.emoji, dashboard.emoji.flags || ""));
        batchCommands.push(createBindCommand(dashboard.tmux, dashboard.tmux.flags || ""));
        batchCommands.push(createBindCommand(dashboard.wallpapers, dashboard.wallpapers.flags || ""));
        batchCommands.push(createBindCommand(dashboard.notes, dashboard.notes.flags || ""));

        // System keybinds
        const system = vibeshell.system;
        unbindCommands.push(createUnbindCommand(system.overview));
        unbindCommands.push(createUnbindCommand(system.powermenu));
        unbindCommands.push(createUnbindCommand(system.config));
        unbindCommands.push(createUnbindCommand(system.lockscreen));
        unbindCommands.push(createUnbindCommand(system.tools));
        unbindCommands.push(createUnbindCommand(system.screenshot));
        unbindCommands.push(createUnbindCommand(system.screenrecord));
        unbindCommands.push(createUnbindCommand(system.lens));
        if (system.reload) unbindCommands.push(createUnbindCommand(system.reload));
        if (system.quit) unbindCommands.push(createUnbindCommand(system.quit));

        batchCommands.push(createBindCommand(system.overview, system.overview.flags || ""));
        batchCommands.push(createBindCommand(system.powermenu, system.powermenu.flags || ""));
        batchCommands.push(createBindCommand(system.config, system.config.flags || ""));
        batchCommands.push(createBindCommand(system.lockscreen, system.lockscreen.flags || ""));
        batchCommands.push(createBindCommand(system.tools, system.tools.flags || ""));
        batchCommands.push(createBindCommand(system.screenshot, system.screenshot.flags || ""));
        batchCommands.push(createBindCommand(system.screenrecord, system.screenrecord.flags || ""));
        batchCommands.push(createBindCommand(system.lens, system.lens.flags || ""));
        if (system.reload) batchCommands.push(createBindCommand(system.reload, system.reload.flags || ""));
        if (system.quit) batchCommands.push(createBindCommand(system.quit, system.quit.flags || ""));

        // Procesar custom keybinds (new format with keys[] and actions[])
        const customBinds = Config.keybindsLoader.adapter.custom;
        if (customBinds && customBinds.length > 0) {
            for (let i = 0; i < customBinds.length; i++) {
                const bind = customBinds[i];

                // Check if bind has the new format
                if (bind.keys && bind.actions) {
                    // Unbind all keys first (always unbind regardless of layout)
                    for (let k = 0; k < bind.keys.length; k++) {
                        unbindCommands.push(createUnbindFromKey(bind.keys[k]));
                    }

                    // Only create binds if enabled
                    if (bind.enabled !== false) {
                        // For each key, bind only compatible actions
                        for (let k = 0; k < bind.keys.length; k++) {
                            for (let a = 0; a < bind.actions.length; a++) {
                                const action = bind.actions[a];
                                // Check if this action is compatible with the current layout
                                if (isActionCompatibleWithLayout(action)) {
                                    batchCommands.push(createBindFromKeyAction(bind.keys[k], action));
                                }
                            }
                        }
                    }
                } else {
                    // Fallback for old format (shouldn't happen after normalization)
                    unbindCommands.push(createUnbindCommand(bind));
                    if (bind.enabled !== false) {
                        const flags = bind.flags || "";
                        batchCommands.push(createBindCommand(bind, flags));
                    }
                }
            }
        }

        storePreviousBinds();

        // Combine unbind and bind in one batch
        const fullBatchCommand = unbindCommands.join("; ") + "; " + batchCommands.join("; ");

        console.log("HyprlandKeybinds: Ejecutando batch command");
        hyprctlProcess.command = ["hyprctl", "--batch", fullBatchCommand];
        hyprctlProcess.running = true;
    }

    property Connections configConnections: Connections {
        target: Config.keybindsLoader
        function onFileChanged() {
            applyKeybinds();
        }
        function onLoaded() {
            applyKeybinds();
        }
        function onAdapterUpdated() {
            applyKeybinds();
        }
    }

    // Re-apply keybinds when layout changes
    property Connections globalStatesConnections: Connections {
        target: GlobalStates
        function onHyprlandLayoutChanged() {
            console.log("HyprlandKeybinds: Layout changed to " + GlobalStates.hyprlandLayout + ", reapplying keybindings...");
            applyKeybinds();
        }
        function onHyprlandLayoutReadyChanged() {
            if (GlobalStates.hyprlandLayoutReady) {
                applyKeybinds();
            }
        }
    }

    property Connections hyprlandConnections: Connections {
        target: Hyprland
        function onRawEvent(event) {
            if (event.name === "configreloaded") {
                console.log("HyprlandKeybinds: Detectado configreloaded, reaplicando keybindings...");
                applyKeybinds();
            }
        }
    }

    Component.onCompleted: {
        // If the loader is already loaded, apply immediately
        if (Config.keybindsLoader.loaded) {
            applyKeybinds();
        }
    }
}
