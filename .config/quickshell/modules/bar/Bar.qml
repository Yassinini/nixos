import QtQuick
import QtQuick.Controls
import QtQuick.Effects
import QtQuick.Layouts
import Quickshell
import Quickshell.Widgets
import Quickshell.Wayland
import qs.modules.bar.workspaces
import qs.modules.theme
import qs.modules.bar.clock
import qs.modules.bar.systray
import qs.modules.widgets.overview
import qs.modules.widgets.dashboard
import qs.modules.widgets.powermenu
import qs.modules.widgets.presets
import qs.modules.corners
import qs.modules.components
import qs.modules.services
import qs.modules.globals
import qs.modules.bar
import qs.config
import "." as Bar

PanelWindow {
    id: panel

    property string barPosition: ["top", "bottom", "left", "right"].includes(Config.bar.position) ? Config.bar.position : "top"
    property string orientation: barPosition === "left" || barPosition === "right" ? "vertical" : "horizontal"
    readonly property bool barEnabled: Config.bar?.enabled ?? true
    readonly property int barThickness: clampInt(Config.bar?.height ?? 44, 24, 80)
    readonly property int barPadding: clampInt(Config.bar?.padding ?? 4, 0, 32)
    readonly property int barMargin: clampInt(Config.bar?.margin ?? 0, 0, 32)
    readonly property int moduleGap: clampInt(Config.bar?.spacing ?? 4, 0, 32)
    readonly property int configuredLength: Config.bar?.width ?? 0
    readonly property int availableLength: orientation === "horizontal" ? Math.max(width - barMargin * 2, 1) : Math.max(height - barMargin * 2, 1)
    readonly property int barLength: configuredLength > 0 ? clampInt(configuredLength, Math.min(200, availableLength), availableLength) : availableLength

    function clampInt(value, minValue, maxValue) {
        return Math.max(minValue, Math.min(maxValue, Math.round(value)));
    }

    readonly property int horizontalAlignedOffset: {
        const free = Math.max(width - (orientation === "horizontal" ? barLength : barThickness), 0);
        return barMargin + Math.max((free - barMargin * 2) / 2, 0);
    }

    readonly property int verticalAlignedOffset: {
        const free = Math.max(height - (orientation === "vertical" ? barLength : barThickness), 0);
        return barMargin + Math.max((free - barMargin * 2) / 2, 0);
    }

    // Auto-hide properties
    property bool pinned: Config.bar?.pinnedOnStartup ?? true

    // Fullscreen detection - check if active toplevel is fullscreen on this screen
    readonly property bool activeWindowFullscreen: {
        const toplevel = ToplevelManager.activeToplevel;
        if (!toplevel || !toplevel.activated)
            return false;
        // Check if the toplevel is fullscreen
        return toplevel.fullscreen === true;
    }

    // Whether auto-hide should be active (not pinned, or fullscreen forces it)
    readonly property bool shouldAutoHide: !pinned || activeWindowFullscreen

    // Hover state with delay to prevent flickering
    property bool hoverActive: false

    // Track if mouse is over bar area
    readonly property bool isMouseOverBar: barMouseArea.containsMouse

    // Check if notch hover is active (for synchronized reveal when bar is at top)
    readonly property var notchPanelRef: Visibilities.notchPanels[screen.name]
    readonly property bool notchHoverActive: {
        if (barPosition !== "top")
            return false;
        // Access the notch panel's hoverActive property if available
        if (notchPanelRef && typeof notchPanelRef.hoverActive !== 'undefined') {
            return notchPanelRef.hoverActive;
        }
        return false;
    }

    // Check if notch is open (dashboard, powermenu, etc.)
    readonly property var screenVisibilities: Visibilities.getForScreen(screen.name)
    readonly property bool notchOpen: screenVisibilities ? (screenVisibilities.dashboard || screenVisibilities.powermenu || screenVisibilities.tools) : false

    // Reveal logic
    readonly property bool reveal: {
        // If not auto-hiding, always reveal
        if (!shouldAutoHide)
            return true;

        // If fullscreen and not available on fullscreen, hide
        if (activeWindowFullscreen && !(Config.bar?.availableOnFullscreen ?? false)) {
            return false;
        }

        // Show if: hovering (when enabled), notch hovering (when at top), notch open, or no active window
        return hoverActive || notchHoverActive || notchOpen || !ToplevelManager.activeToplevel?.activated;
    }

    // Timer to delay hiding the bar after mouse leaves
    Timer {
        id: hideDelayTimer
        interval: 1000
        repeat: false
        onTriggered: {
            if (!panel.isMouseOverBar) {
                panel.hoverActive = false;
            }
        }
    }

    // Watch for mouse state changes
    onIsMouseOverBarChanged: {
        // Only process hover if hoverToReveal is enabled
        if (!(Config.bar?.hoverToReveal ?? true))
            return;

        if (isMouseOverBar) {
            hideDelayTimer.stop();
            hoverActive = true;
        } else {
            hideDelayTimer.restart();
        }
    }

    // Integrated dock configuration
    readonly property bool integratedDockEnabled: (Config.dock?.enabled ?? false) && (Config.dock?.theme ?? "default") === "integrated"
    // Map dock position for integrated based on orientation
    readonly property string integratedDockPosition: {
        const pos = Config.dock?.position ?? "center";

        if (panel.orientation === "horizontal") {
            if (pos === "left" || pos === "start")
                return "start";
            if (pos === "right" || pos === "end")
                return "end";
            return "center";
        }

        // Vertical always falls back to center (or default logic) for now
        // to match the reverted behavior where it ignores start/end.
        return "center";
    }

    anchors {
        top: barPosition !== "bottom"
        bottom: barPosition !== "top"
        left: barPosition !== "right"
        right: barPosition !== "left"
    }

    color: "transparent"
    visible: barEnabled

    WlrLayershell.keyboardFocus: WlrKeyboardFocus.None
    WlrLayershell.layer: WlrLayer.Overlay


    // Reserve space only when revealed and pinned (not in auto-hide mode or fullscreen)
    exclusiveZone: (barEnabled && reveal && pinned && !activeWindowFullscreen) ? (barThickness + barMargin + 6) : 0
    exclusionMode: ExclusionMode.Ignore

    // Implicit height includes extra space for animations/future elements.
    implicitHeight: orientation === "horizontal" ? 200 : Screen.height

    // The mask always points to the MouseArea (same as the Dock)
    mask: Region {
        item: barMouseArea
    }

    Component.onCompleted: {
        Visibilities.registerBar(screen.name, bar);
        Visibilities.registerBarPanel(screen.name, panel);
    }

    Component.onDestruction: {
        Visibilities.unregisterBar(screen.name);
        Visibilities.unregisterBarPanel(screen.name);
    }

    // MouseArea for hover detection - contains bar content (like Dock)
    MouseArea {
        id: barMouseArea
        hoverEnabled: true

        states: [
            State {
                name: "top"
                when: panel.barPosition === "top"
                PropertyChanges {
                    target: barMouseArea
                    x: panel.reveal ? panel.horizontalAlignedOffset : 0
                    y: 0
                    width: panel.reveal ? panel.barLength : panel.width
                    height: panel.reveal ? (bar.height + panel.barMargin) : Math.max(Config.bar?.hoverRegionHeight ?? 8, 4)
                }
            },
            State {
                name: "bottom"
                when: panel.barPosition === "bottom"
                PropertyChanges {
                    target: barMouseArea
                    x: panel.reveal ? panel.horizontalAlignedOffset : 0
                    y: panel.reveal ? (panel.height - bar.height - panel.barMargin) : (panel.height - Math.max(Config.bar?.hoverRegionHeight ?? 8, 4))
                    width: panel.reveal ? panel.barLength : panel.width
                    height: panel.reveal ? (bar.height + panel.barMargin) : Math.max(Config.bar?.hoverRegionHeight ?? 8, 4)
                }
            },
            State {
                name: "left"
                when: panel.barPosition === "left"
                PropertyChanges {
                    target: barMouseArea
                    x: 0
                    y: panel.reveal ? panel.verticalAlignedOffset : 0
                    width: panel.reveal ? (bar.width + panel.barMargin) : Math.max(Config.bar?.hoverRegionHeight ?? 8, 4)
                    height: panel.reveal ? panel.barLength : panel.height
                }
            },
            State {
                name: "right"
                when: panel.barPosition === "right"
                PropertyChanges {
                    target: barMouseArea
                    x: panel.reveal ? (panel.width - bar.width - panel.barMargin) : (panel.width - Math.max(Config.bar?.hoverRegionHeight ?? 8, 4))
                    y: panel.reveal ? panel.verticalAlignedOffset : 0
                    width: panel.reveal ? (bar.width + panel.barMargin) : Math.max(Config.bar?.hoverRegionHeight ?? 8, 4)
                    height: panel.reveal ? panel.barLength : panel.height
                }
            }
        ]

        Behavior on width {
            enabled: Config.animDuration > 0 && panel.shouldAutoHide && panel.orientation === "vertical"
            NumberAnimation {
                duration: Config.animDuration / 4
                easing.type: Easing.OutCubic
            }
        }
        Behavior on height {
            enabled: Config.animDuration > 0 && panel.shouldAutoHide && panel.orientation === "horizontal"
            NumberAnimation {
                duration: Config.animDuration / 4
                easing.type: Easing.OutCubic
            }
        }
        Behavior on y {
            enabled: Config.animDuration > 0 && panel.shouldAutoHide && panel.barPosition === "bottom"
            NumberAnimation {
                duration: Config.animDuration / 4
                easing.type: Easing.OutCubic
            }
        }
        Behavior on x {
            enabled: Config.animDuration > 0 && panel.shouldAutoHide && panel.barPosition === "right"
            NumberAnimation {
                duration: Config.animDuration / 4
                easing.type: Easing.OutCubic
            }
        }

        // Bar content inside MouseArea (clicks pass through to children)
        Item {
            id: bar

            layer.enabled: true
            layer.effect: Shadow {}

            // Opacity animation
            opacity: panel.reveal ? 1 : 0
            Behavior on opacity {
                enabled: Config.animDuration > 0 && panel.shouldAutoHide
                NumberAnimation {
                    duration: Config.animDuration / 2
                    easing.type: Easing.OutCubic
                }
            }

            // Slide animation
            transform: Translate {
                x: {
                    if (!panel.shouldAutoHide)
                        return 0;
                    if (panel.barPosition === "left")
                        return panel.reveal ? 0 : -(bar.width + panel.barMargin);
                    if (panel.barPosition === "right")
                        return panel.reveal ? 0 : (bar.width + panel.barMargin);
                    return 0;
                }
                y: {
                    if (!panel.shouldAutoHide)
                        return 0;
                    if (panel.barPosition === "top")
                        return panel.reveal ? 0 : -(bar.height + panel.barMargin);
                    if (panel.barPosition === "bottom")
                        return panel.reveal ? 0 : (bar.height + panel.barMargin);
                    return 0;
                }
                Behavior on x {
                    enabled: Config.animDuration > 0 && panel.shouldAutoHide
                    NumberAnimation {
                        duration: Config.animDuration / 2
                        easing.type: Easing.OutCubic
                    }
                }
                Behavior on y {
                    enabled: Config.animDuration > 0 && panel.shouldAutoHide
                    NumberAnimation {
                        duration: Config.animDuration / 2
                        easing.type: Easing.OutCubic
                    }
                }
            }

            states: [
                State {
                    name: "top"
                    when: panel.barPosition === "top"
                    AnchorChanges {
                        target: bar
                        anchors.left: undefined
                        anchors.right: undefined
                        anchors.top: parent.top
                        anchors.bottom: undefined
                    }
                    PropertyChanges {
                        target: bar
                        anchors.topMargin: panel.barMargin
                        x: panel.reveal ? 0 : panel.horizontalAlignedOffset
                        width: panel.barLength
                        height: panel.barThickness
                    }
                },
                State {
                    name: "bottom"
                    when: panel.barPosition === "bottom"
                    AnchorChanges {
                        target: bar
                        anchors.left: undefined
                        anchors.right: undefined
                        anchors.top: undefined
                        anchors.bottom: parent.bottom
                    }
                    PropertyChanges {
                        target: bar
                        anchors.bottomMargin: panel.barMargin
                        x: panel.reveal ? 0 : panel.horizontalAlignedOffset
                        width: panel.barLength
                        height: panel.barThickness
                    }
                },
                State {
                    name: "left"
                    when: panel.barPosition === "left"
                    AnchorChanges {
                        target: bar
                        anchors.left: parent.left
                        anchors.right: undefined
                        anchors.top: undefined
                        anchors.bottom: undefined
                    }
                    PropertyChanges {
                        target: bar
                        anchors.leftMargin: panel.barMargin
                        y: panel.reveal ? 0 : panel.verticalAlignedOffset
                        width: panel.barThickness
                        height: panel.barLength
                    }
                },
                State {
                    name: "right"
                    when: panel.barPosition === "right"
                    AnchorChanges {
                        target: bar
                        anchors.left: undefined
                        anchors.right: parent.right
                        anchors.top: undefined
                        anchors.bottom: undefined
                    }
                    PropertyChanges {
                        target: bar
                        anchors.rightMargin: panel.barMargin
                        y: panel.reveal ? 0 : panel.verticalAlignedOffset
                        width: panel.barThickness
                        height: panel.barLength
                    }
                }
            ]

            BarBg {
                id: barBg
                anchors.fill: parent
                position: panel.barPosition
            }

            RowLayout {
                id: horizontalLayout
                visible: panel.orientation === "horizontal"
                anchors.fill: parent
                anchors.topMargin: panel.barPadding
                anchors.bottomMargin: panel.barPadding
                anchors.leftMargin: panel.barPadding + 14
                anchors.rightMargin: panel.barPadding + 14
                spacing: panel.moduleGap

                // Get reference to the notch on this screen
                readonly property var notchContainer: Visibilities.getNotchForScreen(panel.screen.name)

                LauncherButton {
                    id: launcherButton
                    Layout.alignment: Qt.AlignVCenter
                }

                Workspaces {
                    orientation: panel.orientation
                    bar: QtObject {
                        property var screen: panel.screen
                    }
                    Layout.alignment: Qt.AlignVCenter
                }

                LayoutSelectorButton {
                    id: layoutSelectorButton
                    bar: panel
                    layerEnabled: Config.showBackground
                    Layout.alignment: Qt.AlignVCenter
                }

                // Pin button (horizontal)
                Loader {
                    active: Config.bar?.showPinButton ?? true
                    visible: active
                    Layout.alignment: Qt.AlignVCenter

                    sourceComponent: Button {
                        id: pinButton
                        implicitWidth: 36
                        implicitHeight: 36
                        padding: 0
                        topPadding: 0
                        bottomPadding: 0
                        leftPadding: 0
                        rightPadding: 0

                        background: StyledRect {
                            id: pinButtonBg
                            variant: panel.pinned ? "primary" : "bg"
                            enableShadow: Config.showBackground
                            Rectangle {
                                anchors.fill: parent
                                color: Styling.srItem("overprimary")
                                opacity: panel.pinned ? 0 : (pinButton.pressed ? 0.5 : (pinButton.hovered ? 0.25 : 0))
                                radius: parent.radius ?? 0

                                Behavior on opacity {
                                    enabled: (Config.animDuration ?? 0) > 0
                                    NumberAnimation {
                                        duration: (Config.animDuration ?? 0) / 2
                                    }
                                }
                            }
                        }

                        contentItem: Text {
                            text: Icons.pin
                            font.family: Icons.font
                            font.pixelSize: 18
                            color: panel.pinned ? pinButtonBg.item : (pinButton.pressed ? Colors.background : (Styling.srItem("overprimary") || Colors.foreground))
                            horizontalAlignment: Text.AlignHCenter
                            verticalAlignment: Text.AlignVCenter

                            rotation: panel.pinned ? 0 : 45
                            Behavior on rotation {
                                enabled: Config.animDuration > 0
                                NumberAnimation {
                                    duration: Config.animDuration / 2
                                }
                            }

                            Behavior on color {
                                enabled: Config.animDuration > 0
                                ColorAnimation {
                                    duration: Config.animDuration / 2
                                }
                            }
                        }

                        onClicked: panel.pinned = !panel.pinned

                        StyledToolTip {
                            show: pinButton.hovered
                            tooltipText: panel.pinned ? "Unpin bar" : "Pin bar"
                        }
                    }
                }

                Item {
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    visible: panel.orientation === "horizontal" && integratedDockEnabled

                    Bar.IntegratedDock {
                        bar: panel
                        orientation: panel.orientation
                        anchors.verticalCenter: parent.verticalCenter

                        // Calculate target position based on config
                        property real targetX: {
                            if (integratedDockPosition === "start")
                                return 0;
                            if (integratedDockPosition === "end")
                                return parent.width - width;

                            // Center logic (reactive using parent.x + margin offset)
                            // RowLayout has anchors.margins: 4, so offset is 4
                            return (bar.width - width) / 2 - (parent.x + 4);
                        }

                        // Clamp the x position so it never leaves the container (preventing overlap)
                        x: Math.max(0, Math.min(parent.width - width, targetX))

                        width: Math.min(implicitWidth, parent.width)
                        height: implicitHeight
                    }
                }

                Item {
                    Layout.fillWidth: true
                    visible: !(panel.orientation === "horizontal" && integratedDockEnabled)
                }

                PresetsButton {
                    id: presetsButton
                    Layout.alignment: Qt.AlignVCenter
                }

                Bar.WallpaperButton {
                    id: wallpaperButton
                    Layout.alignment: Qt.AlignVCenter
                }

                ToolsButton {
                    id: toolsButton
                    Layout.alignment: Qt.AlignVCenter
                }

                Bar.NotesButton {
                    id: notesButton
                    Layout.alignment: Qt.AlignVCenter
                }

                Bar.MonitorButton {
                    id: monitorButton
                    Layout.alignment: Qt.AlignVCenter
                }

                SysTray {
                   bar: panel
                   layer.enabled: Config.showBackground
                   Layout.alignment: Qt.AlignVCenter
                }

                ControlsButton {
                    id: controlsButton
                    bar: panel
                    layerEnabled: Config.showBackground
                    Layout.alignment: Qt.AlignVCenter
                }

                Clock {
                    id: clockComponent
                    bar: panel
                    layerEnabled: Config.showBackground
                    Layout.alignment: Qt.AlignVCenter
                }

                Bar.SettingsButton {
                    id: settingsButton
                    Layout.alignment: Qt.AlignVCenter
                }

                ToggleButton {
                    id: powerButton
                    Layout.preferredWidth: 36
                    Layout.preferredHeight: 36
                    visible: true
                    Layout.alignment: Qt.AlignVCenter
                    buttonIcon: Icons.shutdown
                    tooltipText: "Power Menu"
                    onToggle: function () {
                        if (Visibilities.currentActiveModule === "powermenu") {
                            Visibilities.setActiveModule("");
                        } else {
                            Visibilities.setActiveModule("powermenu");
                        }
                    }
                }
            }

            ColumnLayout {
                id: verticalLayout
                visible: panel.orientation === "vertical"
                anchors.fill: parent
                anchors.leftMargin: panel.barPadding
                anchors.rightMargin: panel.barPadding
                anchors.topMargin: panel.barPadding + 14
                anchors.bottomMargin: panel.barPadding + 14
                spacing: panel.moduleGap

                LauncherButton {
                    id: launcherButtonVert
                    Layout.preferredHeight: 36
                    Layout.alignment: Qt.AlignHCenter
                }

                SysTray {
                    bar: panel
                    layer.enabled: Config.showBackground
                    Layout.alignment: Qt.AlignHCenter
                }

                ToolsButton {
                    id: toolsButtonVert
                    Layout.alignment: Qt.AlignHCenter
                }

                Bar.NotesButton {
                    id: notesButtonVert
                    Layout.preferredHeight: 36
                    Layout.alignment: Qt.AlignHCenter
                }

                Bar.MonitorButton {
                    id: monitorButtonVert
                    Layout.preferredHeight: 36
                    Layout.alignment: Qt.AlignHCenter
                }

                PresetsButton {
                    id: presetsButtonVert
                    Layout.alignment: Qt.AlignHCenter
                }

                Bar.WallpaperButton {
                    id: wallpaperButtonVert
                    Layout.preferredHeight: 36
                    Layout.alignment: Qt.AlignHCenter
                }

                // Center Group Container
                Item {
                    Layout.fillHeight: true
                    Layout.fillWidth: true

                    ColumnLayout {
                        anchors.horizontalCenter: parent.horizontalCenter

                        // Calculate target position to be absolutely centered in the bar (vertically)
                        property real targetY: {
                            if (!parent || !bar)
                                return 0;
                            var parentPos = parent.mapToItem(bar, 0, 0);
                            return (bar.height - height) / 2 - parentPos.y;
                        }

                        // Clamp y position
                        y: Math.max(0, Math.min(parent.height - height, targetY))

                        height: Math.min(parent.height, implicitHeight)
                        width: parent.width
                        spacing: 4

                        LayoutSelectorButton {
                            id: layoutSelectorButtonVert
                            bar: panel
                            layerEnabled: Config.showBackground
                            Layout.alignment: Qt.AlignHCenter
                        }

                        Workspaces {
                            id: workspacesVert
                            orientation: panel.orientation
                            bar: QtObject {
                                property var screen: panel.screen
                            }
                            Layout.alignment: Qt.AlignHCenter
                        }

                        // Pin button (vertical)
                        Loader {
                            active: Config.bar?.showPinButton ?? true
                            visible: active
                            Layout.alignment: Qt.AlignHCenter

                            sourceComponent: Button {
                                id: pinButtonV
                                implicitWidth: 36
                                implicitHeight: 36
                                padding: 0
                                topPadding: 0
                                bottomPadding: 0
                                leftPadding: 0
                                rightPadding: 0

                                background: StyledRect {
                                    id: pinButtonVBg
                                    variant: panel.pinned ? "primary" : "bg"
                                    enableShadow: Config.showBackground
                                    Rectangle {
                                        anchors.fill: parent
                                        color: Styling.srItem("overprimary")
                                        opacity: panel.pinned ? 0 : (pinButtonV.pressed ? 0.5 : (pinButtonV.hovered ? 0.25 : 0))
                                        radius: parent.radius ?? 0

                                        Behavior on opacity {
                                            enabled: (Config.animDuration ?? 0) > 0
                                            NumberAnimation {
                                                duration: (Config.animDuration ?? 0) / 2
                                            }
                                        }
                                    }
                                }

                                contentItem: Text {
                                    text: Icons.pin
                                    font.family: Icons.font
                                    font.pixelSize: 18
                                    color: panel.pinned ? pinButtonVBg.item : (pinButtonV.pressed ? Colors.background : (Styling.srItem("overprimary") || Colors.foreground))
                                    horizontalAlignment: Text.AlignHCenter
                                    verticalAlignment: Text.AlignVCenter

                                    rotation: panel.pinned ? 0 : 45
                                    Behavior on rotation {
                                        enabled: Config.animDuration > 0
                                        NumberAnimation {
                                            duration: Config.animDuration / 2
                                        }
                                    }

                                    Behavior on color {
                                        enabled: Config.animDuration > 0
                                        ColorAnimation {
                                            duration: Config.animDuration / 2
                                        }
                                    }
                                }

                                onClicked: panel.pinned = !panel.pinned

                                StyledToolTip {
                                    show: pinButtonV.hovered
                                    tooltipText: panel.pinned ? "Unpin bar" : "Pin bar"
                                }
                            }
                        }

                        Bar.IntegratedDock {
                            bar: panel
                            orientation: panel.orientation
                            visible: integratedDockEnabled
                            Layout.fillHeight: true
                            Layout.fillWidth: true
                        }
                    }
                }

                ControlsButton {
                    id: controlsButtonVert
                    bar: panel
                    layerEnabled: Config.showBackground
                    Layout.alignment: Qt.AlignHCenter
                }

                Clock {
                    id: clockComponentVert
                    bar: panel
                    layerEnabled: Config.showBackground
                    Layout.alignment: Qt.AlignHCenter
                }

                Bar.SettingsButton {
                    id: settingsButtonVert
                    Layout.preferredHeight: 36
                    Layout.alignment: Qt.AlignHCenter
                }

                ToggleButton {
                    id: powerButtonVert
                    Layout.preferredWidth: 36
                    Layout.preferredHeight: 36
                    visible: true
                    buttonIcon: Icons.shutdown
                    tooltipText: "Power Menu"
                    Layout.alignment: Qt.AlignHCenter
                    onToggle: function () {
                        if (Visibilities.currentActiveModule === "powermenu") {
                            Visibilities.setActiveModule("");
                        } else {
                            Visibilities.setActiveModule("powermenu");
                        }
                    }
                }
            }
        }
    }
}
